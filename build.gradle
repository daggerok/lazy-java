buildscript {
  ext {
    javaVersion = '1.8'
    vavrVersion = '0.9.2'
    slf4jVersion = '1.7.25'
    lombokVersion = '1.18.2'
    junitJupiterVersion = '5.3.1'
    junitPlatformVersion = '1.3.0'
  }
}

plugins {
  id 'idea'
  id 'java'
  id 'maven'
  id 'eclipse'
  id 'application'
  id 'com.github.johnrengelman.shadow' version '4.0.1'
  id 'com.avast.gradle.docker-compose' version '0.8.8'
}

group = 'daggerok'
version = '0.0.2'
sourceCompatibility = targetCompatibility = "$javaVersion"

mainClassName = 'daggerok.App'
shadowJar.mustRunAfter jar

defaultTasks 'clean', 'build'

repositories {
  mavenLocal()
  mavenCentral()
}

dependencies {

  implementation "io.vavr:vavr:$vavrVersion"

  implementation "org.slf4j:slf4j-api:$slf4jVersion"
  implementation 'ch.qos.logback:logback-classic:1.2.3'
  compileOnly "org.projectlombok:lombok:$lombokVersion"

  testCompileOnly("org.projectlombok:lombok:$lombokVersion")
}

dockerCompose {
  useComposeFiles = [project.file('docker-compose-gradle.yaml')]
  captureContainersOutput = true
  stopContainers = true
  removeContainers = true
  removeImages = 'Local'
  removeVolumes = true
  removeOrphans = true
  projectName = project.name
}

composeUp.dependsOn assemble

wrapper {
  gradleVersion = '4.10.2'
  distributionType = 'BIN'
}

dependencies {
  testImplementation 'junit:junit:4.12'
  testImplementation 'org.assertj:assertj-core:3.11.1'
  testImplementation 'org.hamcrest:hamcrest-core:1.3'
  testImplementation "org.junit.jupiter:junit-jupiter-api:$junitJupiterVersion"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitJupiterVersion"
  testRuntimeOnly "org.junit.vintage:junit-vintage-engine:$junitJupiterVersion", {
    because 'allows JUnit 3 and JUnit 4 tests to run'
  }
  testRuntime "org.junit.platform:junit-platform-launcher:$junitPlatformVersion", {
    because 'allows tests to run from IDEs that bundle older version of launcher'
  }
  testCompileOnly 'org.apiguardian:apiguardian-api:1.0.0'
}

test {
  useJUnitPlatform {
    includeEngines 'junit-jupiter', 'junit-vintage'
  }
}
